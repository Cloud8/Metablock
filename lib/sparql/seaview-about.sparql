PREFIX  rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX  dcterms:   <http://purl.org/dc/terms/>
PREFIX  dctypes: <http://purl.org/dc/dcmitype/>
PREFIX  foaf:  <http://xmlns.com/foaf/0.1/>
PREFIX  fabio: <http://purl.org/spar/fabio/>
PREFIX  aiiso: <http://purl.org/vocab/aiiso/schema#>
PREFIX  frbr: <http://purl.org/vocab/frbr/core#>
PREFIX  pica: <http://localhost/view/>
PREFIX  daia: <http://purl.org/ontology/daia/>
PREFIX  pro: <http://purl.org/spar/pro/>
PREFIX  skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX  prism: <http://prismstandard.org/namespaces/basic/2.0/>
PREFIX  cito: <http://purl.org/spar/cito/>
PREFIX  ore: <http://www.openarchives.org/ore/terms/>
PREFIX  void: <http://rdfs.org/ns/void#>

## six levels of hierarchy
construct {
    <subject> ?b ?c .
    ?c ?d ?e .
    ?e ?f ?g .
    ?g ?h ?i .
    ?i ?j ?k .
} where { 
    {
      <subject> ?b ?c .
      optional {
        ?c ?d ?e .
         optional {
             ?e ?f ?g .
             optional {
                 ?g ?h ?i .
                 optional {
                     ?i ?j ?k .
                 }
             }
         }
      }
      FILTER (?b NOT IN (dcterms:isPartOf))
      FILTER (?b NOT IN (dcterms:hasPart))
      FILTER (?b NOT IN (dcterms:isReferencedBy))
      FILTER (NOT EXISTS { ?c a void:Dataset})
    } union {
      <subject> ?b ?c .
      values ?b { dcterms:isPartOf }
      optional {
        ?c ?d ?e .
        FILTER (?d NOT IN (dcterms:hasPart))
        optional {
            ?e ?f ?g .
            FILTER (?f NOT IN (dcterms:hasPart))
        }
      }
    } union {
      <subject> ?b ?c .
      values ?b { dcterms:hasPart dcterms:isReferencedBy}
    } union {
      <subject> ?b ?c .
      values ?b { dcterms:hasPart }
      { ?c a dctypes:Text }
        union { ?c a dctypes:Dataset }
        union { ?c a dctypes:Collection }
        union { ?c a dctypes:Image }
        union { ?c a dctypes:MovingImage }
      optional {
        ?c ?d ?e .
         optional {
             ?e ?f ?g .
        }
      }
    } 
} 
